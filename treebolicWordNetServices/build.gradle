/*
 * Copyright (c) Treebolic 2019. Bernard Bou <1313ou@gmail.com>
 */

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    namespace "org.treebolic.wordnet"

    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "org.treebolic.wordnet"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
    }

    signingConfigs {
        treebolic {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.treebolic
            versionNameSuffix 'signed'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'io.github.treebolic:treebolic-model:4.1-3'
    implementation 'io.github.treebolic:provider-wordnet-jwi:4.1-3'

    implementation project(':treebolicServicesIface')
    implementation project(':treebolicServicesLib')
    implementation project(':treebolicClientsIface')
    implementation project(':treebolicClientsLib')
    implementation project(':treebolicIface')
    implementation project(':treebolicParcel')

    implementation project(':commonLib')
    implementation project(':downloadLib')
    implementation project(':preferenceLib')
    implementation project(':rateLib')
    implementation project(':othersLib')
    implementation project(':donateLib')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'org.apache.commons:commons-compress:1.25.0'

    implementation "androidx.multidex:multidex:${multiDexVersion}"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${desugaringVersion}"

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
